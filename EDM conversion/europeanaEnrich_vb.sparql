PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX dc: <http://purl.org/dc/elements/1.1/>
PREFIX edm: <http://www.europeana.eu/schemas/edm/>
PREFIX crm: <http://www.cidoc-crm.org/cidoc-crm/>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX wd: <http://www.wikidata.org/entity/>
PREFIX p: <http://www.wikidata.org/prop/>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX wikibase: <http://wikiba.se/ontology#>
PREFIX bd: <http://www.bigdata.com/rdf#>
PREFIX idm: <https://www.intavia.org/idm/>
PREFIX idmrole: <https://www.intavia.org/idm/role/>
PREFIX bioc: <http://ldf.fi/schema/bioc/>
PREFIX psv: <http://www.wikidata.org/prop/statement/value/>
PREFIX schema: <http://schema.org/>
PREFIX psn: <http://www.wikidata.org/prop/statement/value-normalized/>
PREFIX wds: <http://www.wikidata.org/entity/statement/>
PREFIX pq: <http://www.wikidata.org/prop/qualifier/>

CONSTRUCT {
	?cho a crm:E24_Physical_Human_Made_Thing,
		idm:CHO_Proxy;
		crm:P1_is_identified_by ?cho_1476Title;
		bioc:bearer_of ?choProductionEventRole.
	?cho rdfs:label ?dc_title .

	?edm_object crm:P70_documents ?cho;
		a crm:E31_Document.
	?cho_1476Title rdfs:label ?dc_title;
		a crm:E35_Title .

	?choProductionEventRole a bioc:Thing_Role .
	?choProductionEvent bioc:occured_in_the_presence_of_in_role ?choProductionEventRole ;
		 a crm:E12_Production ;
		 crm:P4_has_time-span ?choProductionTimespan ;
		 rdfs:label ?eventLabel .

	?choProductionTimespan rdfs:label ?dc_date;
		crm:P82a_beginning_of_the_begin ?dc_date ;
		crm:P82b_end_of_the_end ?dc_date .

	?choProductionEvent crm:P7_took_place_at ?locationofcreation.
	?locationofcreation a crm:E53_Place .
	?choProductionEvent bioc:had_participant_in_role ?producingArtistRole .
	?artist bioc:bearer_of ?producingArtistRole .
	?producingArtistRole a idmrole:producing_artist .
	idmrole:producing_artist rdfs:label "producing artist"@en
}
WHERE 
{  		
	?dc_creator_uri owl:sameAs ?artistUri .
	?cho dc:creator ?dc_creator_uri .

	BIND(REPLACE(STR(?cho), "http://", "https://www.intavia.eu/productionthingrole/") as ?choProductionEventRoleStr)      
	BIND(IRI(?choProductionEventRoleStr) as ?choProductionEventRole)
	BIND(REPLACE(STR(?cho), "http://", "https://www.intavia.eu/production_event/") as ?choProductionEventStr)      
	BIND(IRI(?choProductionEventStr) as ?choProductionEvent)
	BIND(IRI(REPLACE(STR(?artistUri), "http://", STR(idmrole:producing_artist))) as ?producingArtistRole)
	BIND(REPLACE(STR(?cho), "http://", "https://www.intavia.org/chotitle/") as ?cho_TitleStr)
	BIND(IRI(?cho_TitleStr) as ?cho_1476Title)
	OPTIONAL {?cho dc:coverage ?dc_coverage_uri}
	OPTIONAL {?cho dc:type ?dc_type_uri }

	# Shortcut technique to avoid hopping many nodes (for the 2 subqueries below and )
	BIND(IRI(REPLACE(str(?cho), "http://data.europeana.eu/proxy/europeana/", "http://data.europeana.eu/proxy/provider/")) AS ?provider_proxy)
	BIND(IRI(REPLACE(str(?cho), "http://data.europeana.eu/proxy/europeana/", "http://data.europeana.eu/aggregation/provider/")) AS ?provider_agg)
	BIND(IRI(REPLACE(str(?cho), "http://data.europeana.eu/proxy/europeana/", "http://data.europeana.eu/item/")) AS ?item)

	?provider_proxy edm:type ?edm_type
	OPTIONAL {?provider_proxy dc:creator ?dc_creator}
	OPTIONAL {?provider_proxy dc:coverage ?dc_coverage.
		BIND(REPLACE(STR(?cho), "http://", "https://www.intavia.eu/location_of_production/") as ?locationofcreation)      
	}
	OPTIONAL {?provider_proxy dc:date ?dc_date}
	OPTIONAL {?provider_proxy dc:title ?dc_title}
	OPTIONAL {?provider_agg edm:object ?edm_object}


}
